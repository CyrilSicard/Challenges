*> THE SAMPLE CODE BELOW OUTPUTS WHAT IS READ ON STDIN
*> use DISPLAY "Hello stderr world" UPON SYSERR to output debugging information to stderr
IDENTIFICATION DIVISION.
PROGRAM-ID. APP.

ENVIRONMENT DIVISION.
INPUT-OUTPUT SECTION.
FILE-CONTROL.

    SELECT SYSIN ASSIGN TO KEYBOARD ORGANIZATION LINE SEQUENTIAL.

DATA DIVISION.
FILE SECTION.

FD SYSIN.
01 ln PIC X(50000).
    88 EOF VALUE HIGH-VALUES.

WORKING-STORAGE SECTION.

01 NB-BATIMENTS PIC 9(05).
01 NB-ROUTES    PIC 9(05).
01 NB-TRICHEURS PIC 9(05).

01 WS-BATIMENTS-RESTANT PIC 9(05).

01 WS-LECTURE.
    05 OCCURS 3 TIMES.
        10 LEC-TMP-VAL  PIC X(05).

01 WS-T-POS PIC 9(05).

01.
    05  OCCURS 1 TO 10000 TIMES DEPENDING ON NB-BATIMENTS INDEXED BY IDX-BATIMENT.
        10 BAT-OCCUPANT PIC X VALUE " ".

01.
    05  OCCURS 1 TO 20000 TIMES DEPENDING ON NB-ROUTES INDEXED BY IDX-ROUTE.
        10 BAT-SRC  PIC 9(05).
        10 BAT-DST  PIC 9(05).

01 WS-BATIMENT  PIC Z(04)9.

01 WS-OUTPUT    PIC X(50000).

PROCEDURE DIVISION.

    DISPLAY "===== START PROCEDURE =====" UPON SYSERR

    OPEN INPUT SYSIN
    READ SYSIN
      AT END SET EOF TO TRUE
    END-READ

    UNSTRING ln DELIMITED BY SPACE INTO
        LEC-TMP-VAL(1)
        LEC-TMP-VAL(2)
        LEC-TMP-VAL(3)
    END-UNSTRING
    MOVE FUNCTION NUMVAL(LEC-TMP-VAL(1)) TO NB-BATIMENTS
    MOVE FUNCTION NUMVAL(LEC-TMP-VAL(2)) TO NB-ROUTES
    MOVE FUNCTION NUMVAL(LEC-TMP-VAL(3)) TO NB-TRICHEURS

    READ SYSIN
        AT END SET EOF TO TRUE
    END-READ
    PERFORM LIRE-TRICHEURS THRU FIN-LIRE-TRICHEURS
    MOVE "V" TO BAT-OCCUPANT(1)

    READ SYSIN
        AT END SET EOF TO TRUE
    END-READ
    MOVE 1 TO IDX-ROUTE
    PERFORM UNTIL EOF

        UNSTRING ln DELIMITED BY SPACE INTO
            LEC-TMP-VAL(1)
            LEC-TMP-VAL(2)
        END-UNSTRING
        MOVE FUNCTION NUMVAL(LEC-TMP-VAL(1)) TO BAT-SRC(IDX-ROUTE)
        MOVE FUNCTION NUMVAL(LEC-TMP-VAL(2)) TO BAT-DST(IDX-ROUTE)
        ADD 1 TO IDX-ROUTE

        READ SYSIN
            AT END SET EOF TO TRUE
        END-READ
    END-PERFORM
    CLOSE SYSIN

    MOVE NB-BATIMENTS TO WS-BATIMENTS-RESTANT
    PERFORM UNTIL WS-BATIMENTS-RESTANT EQUAL ZERO

        PERFORM VARYING IDX-ROUTE FROM 1 BY 1 UNTIL IDX-ROUTE > NB-ROUTES
            IF BAT-OCCUPANT(BAT-SRC(IDX-ROUTE)) = "T" AND BAT-OCCUPANT(BAT-DST(IDX-ROUTE)) = " " THEN
                MOVE "C" TO BAT-OCCUPANT(BAT-DST(IDX-ROUTE))
            END-IF
            IF BAT-OCCUPANT(BAT-SRC(IDX-ROUTE)) = " " AND BAT-OCCUPANT(BAT-DST(IDX-ROUTE)) = "T" THEN
                MOVE "C" TO BAT-OCCUPANT(BAT-SRC(IDX-ROUTE))
            END-IF
        END-PERFORM

        PERFORM VARYING IDX-ROUTE FROM 1 BY 1 UNTIL IDX-ROUTE > NB-ROUTES
            IF BAT-OCCUPANT(BAT-SRC(IDX-ROUTE)) = "V" AND BAT-OCCUPANT(BAT-DST(IDX-ROUTE)) = " " THEN
                MOVE "H" TO BAT-OCCUPANT(BAT-DST(IDX-ROUTE))
            END-IF
            IF BAT-OCCUPANT(BAT-SRC(IDX-ROUTE)) = " " AND BAT-OCCUPANT(BAT-DST(IDX-ROUTE)) = "V" THEN
                MOVE "H" TO BAT-OCCUPANT(BAT-SRC(IDX-ROUTE))
            END-IF
        END-PERFORM

        MOVE ZERO TO WS-BATIMENTS-RESTANT
        PERFORM VARYING IDX-BATIMENT FROM 1 BY 1 UNTIL IDX-BATIMENT > NB-BATIMENTS
            IF BAT-OCCUPANT(IDX-BATIMENT) = "C" THEN
                MOVE "T" TO BAT-OCCUPANT(IDX-BATIMENT)
            END-IF
            IF BAT-OCCUPANT(IDX-BATIMENT) = "H" THEN
                MOVE "V" TO BAT-OCCUPANT(IDX-BATIMENT)
            END-IF
            IF BAT-OCCUPANT(IDX-BATIMENT) = " " THEN
                ADD 1 TO WS-BATIMENTS-RESTANT
            END-IF
        END-PERFORM
    END-PERFORM

    PERFORM ECRIRE-VAINQUEUR THRU FIN-ECRIRE-VAINQUEUR
    DISPLAY WS-OUTPUT

    STOP RUN.

ECRIRE-VAINQUEUR.

    MOVE 1 TO WS-T-POS.

    PERFORM VARYING IDX-BATIMENT FROM 1 BY 1 UNTIL IDX-BATIMENT > NB-BATIMENTS
        IF BAT-OCCUPANT(IDX-BATIMENT) = "V" THEN
            MOVE IDX-BATIMENT TO WS-BATIMENT
            STRING
                FUNCTION TRIM(WS-BATIMENT) DELIMITED BY LOW-VALUE
                SPACE DELIMITED BY SIZE
                INTO WS-OUTPUT
                WITH POINTER WS-T-POS
            END-STRING
        END-IF
    END-PERFORM.

FIN-ECRIRE-VAINQUEUR.
    EXIT.

LIRE-TRICHEURS.

    MOVE 1 TO WS-T-POS.

    PERFORM NB-TRICHEURS TIMES
        UNSTRING ln DELIMITED BY SPACE
            INTO LEC-TMP-VAL(1)
            WITH POINTER WS-T-POS
        END-UNSTRING
        MOVE FUNCTION NUMVAL(LEC-TMP-VAL(1)) TO IDX-BATIMENT
        MOVE "T"                             TO BAT-OCCUPANT(IDX-BATIMENT)
    END-PERFORM.

FIN-LIRE-TRICHEURS.
    EXIT.
